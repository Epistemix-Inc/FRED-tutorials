
place School {
    has_group_agent = 1
}

variables {
    shared numeric NO_CLOSURE
    NO_CLOSURE = 0
    
    shared numeric LOCAL_CLOSURE
    LOCAL_CLOSURE = 1

    shared numeric GLOBAL_CLOSURE
    GLOBAL_CLOSURE = 2

    shared numeric school_closure_policy
    school_closure_policy = NO_CLOSURE
    
    shared numeric days_closed
    days_closed = 7

    shared numeric global_closure_trigger
    global_closure_trigger = 1000

    shared numeric local_closure_trigger
    local_closure_trigger = 20
}


condition SCHOOL {
    group_start_state = Admin_Start
    start_state = Start

	state Admin_Start {
        wait(0)
        if (is_group_agent(School)) then next(CheckEpidemic)
        default(Excluded)
    }

    state CheckEpidemic {
        wait(0)
    	if (school_closure_policy == LOCAL_CLOSURE) then next(CheckLocalEpidemic)
    	if (school_closure_policy == GLOBAL_CLOSURE) then next(CheckGlobalEpidemic)
        default(CheckCalendar)
    }

    state CheckLocalEpidemic {
        wait(0)
	    if (local_closure_trigger <= current_count(INF.Is,School)) then next(Close)
        default(CheckCalendar)
    }

    state CheckGlobalEpidemic {
        wait(0)
	    if (global_closure_trigger <= current_count(INF.Is)) then next(Close)
        default(CheckCalendar)
    }

    state CheckCalendar {
        wait(0)
        if (is_date_in_range(Dec-20,Jan-02)) then next(WinterBreak) 
        if (is_date_in_range(Mar-10,Mar-15)) then next(SpringBreak)
        if (is_date_in_range(Jun-15,Aug-25)) then next(SummerBreak)
        default(Open)
    }

    state Open {
	    reopen()
        wait(24)
        default(CheckEpidemic)
    }

    state Close {
        close()
        wait(24*days_closed)
        next(CheckEpidemic)
    }

    state WinterBreak {
        close()
        wait(until(Jan,03))
        next(CheckEpidemic)
    }

    state SpringBreak {
        close()
        wait(until(Mar,16))
        next(CheckEpidemic)
    }

    state SummerBreak {
        close()
        wait(until(Aug,26))
        next(CheckEpidemic)
    }

	state Start {
	   wait(0)
	   if (is_member(School) & age < 18) then next(StudentSchoolOpen)
	   next(Excluded)	   
	}

    state StudentSchoolOpen {
        wait(24)
 		if (is_temporarily_closed(School)) then next(StudentSchoolClosed)
        default(StudentSchoolOpen)
    }

    state StudentSchoolClosed {
        wait(24)
 		if (is_temporarily_closed(School)) then next(StudentSchoolClosed)
        default(StudentSchoolOpen)
    }

}

