
place School {
    has_administrator = 1
}

variables {
    global NO_CLOSURE
    NO_CLOSURE = 0
    
    global LOCAL_CLOSURE
    LOCAL_CLOSURE = 1

    global GLOBAL_CLOSURE
    GLOBAL_CLOSURE = 2

    global school_closure_policy
    school_closure_policy = NO_CLOSURE
    
    global days_closed
    days_closed = 28

    global global_closure_trigger
    global_closure_trigger = 1000

    global local_closure_trigger
    local_closure_trigger = 20
}


condition SCHOOL {
    admin_start_state = Start

    state Start {
        if (admin(School)) then next(CheckEpidemic)
        if (profile == student) then next(StudentSchoolOpen)
        default(Excluded)
    }

    state CheckEpidemic {
        wait(0)
    	if (school_closure_policy == LOCAL_CLOSURE) then next(CheckLocalEpidemic)
    	if (school_closure_policy == GLOBAL_CLOSURE) then next(CheckGlobalEpidemic)
        default(CheckCalendar)
    }

    state CheckLocalEpidemic {
        wait(0)
	if (local_closure_trigger <= current_count_of_INF.Is_in_School) then next(Close)
        default(CheckCalendar)
    }

    state CheckGlobalEpidemic {
        wait(0)
	if (global_closure_trigger <= current_count_of_INF.Is) then next(Close)
        default(CheckCalendar)
    }

    state CheckCalendar {
        wait(0)
        if (date_range(Dec-20,Jan-02)) then next(WinterBreak) 
        if (date_range(Mar-10,Mar-15)) then next(SpringBreak)
        if (date_range(Jun-15,Aug-25)) then next(SummerBreak)
        default(Open)
    }

    state Open {
        wait(24)
        default(CheckEpidemic)
    }

    state Close {
        close(School)
        wait(24*days_closed)
        next(CheckEpidemic)
    }

    state WinterBreak {
        close(School)
        wait(until(Jan,03))
        next(CheckEpidemic)
    }

    state SpringBreak {
        close(School)
        wait(until(Mar,16))
        next(CheckEpidemic)
    }

    state SummerBreak {
        close(School)
        wait(until(Aug,26))
        next(CheckEpidemic)
    }

    state StudentSchoolOpen {
        wait(24)
        if (has_been_closed(School)) then next(StudentSchoolClosed)
        default(StudentSchoolOpen)
    }

    state StudentSchoolClosed {
        wait(24)
        if (has_been_closed(School)) then next(StudentSchoolClosed)
        default(StudentSchoolOpen)
    }
}

